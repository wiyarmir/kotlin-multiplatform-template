import groovy.json.StringEscapeUtils

plugins {
    id 'org.jetbrains.kotlin.js'
}

repositories {
    jcenter()
}

dependencies {
    implementation project(":common")

    implementation Libs.kotlinStdlibJs
    testImplementation Libs.kotlinTestJs

    implementation Libs.kotlinxHtmlJs
    implementation Libs.kotlinxCoroutinesCoreCommon
    implementation Libs.kotlinxCoroutinesCoreJs
    implementation Libs.kotlinxSerializationRuntimeJs

    implementation Libs.kotlinExtensions
    implementation Libs.kotlinReact
    implementation Libs.kotlinReactDom
    implementation npm('react', Versions.react)
    implementation npm('react-dom', Versions.react)

    // https://github.com/JetBrains/kotlin-wrappers/issues/109
    implementation npm("core-js", "3")

    // https://github.com/ktorio/ktor/issues/961
    implementation npm("text-encoding", "*")

    // devDependencies but no dsl
    implementation npm("webpack", "4")
    implementation npm("babel-loader", "8")
    implementation npm("@babel/core", "7")
    implementation npm("@babel/preset-env", "7")
    implementation npm("css-loader", "*")
    implementation npm("style-loader", "*")
    implementation npm("html-loader", "*")
    implementation npm("source-map-loader", "*")
    implementation npm("karma", "*")
}

kotlin {
    target {
        browser {
            webpackTask {
                doFirst {
                    def path = StringEscapeUtils.escapeJavaScript(file("src/main/resources").absolutePath)
                    file("webpack.config.d/resources.js").text = "config.resolve.modules.push('$path');"
                }
            }
        }
    }
}

compileKotlinJs {
    kotlinOptions {
        metaInfo = true
        outputFile = "$project.buildDir.path/js/${project.name}.js"
        sourceMap = true
        sourceMapEmbedSources = "always"
        moduleKind = 'commonjs'
        main = "call"
    }
}
