import groovy.json.StringEscapeUtils

plugins {
    id 'org.jetbrains.kotlin.js'
}

repositories {
    jcenter()
}

dependencies {
    implementation project(":common")

    testImplementation Libs.kotlinTestJs

    implementation Libs.kotlinxHtmlJs
    implementation Libs.kotlinxCoroutinesCoreCommon

    implementation Libs.kotlinExtensions
    implementation Libs.kotlinReact
    implementation Libs.kotlinReactDom
    implementation npm('react', Versions.react)
    implementation npm('react-dom', Versions.react)

    // https://github.com/JetBrains/kotlin-wrappers/issues/109
    implementation npm("core-js", "3")

    // https://github.com/ktorio/ktor/issues/961
    implementation npm("text-encoding", "*")

    implementation(npm("abort-controller", "3.0.0"))

    // devDependencies but no dsl
    implementation devNpm("webpack", "4.42.1")
    implementation devNpm("babel-loader", "8")
    implementation devNpm("@babel/core", "7")
    implementation devNpm("@babel/preset-env", "7")
    implementation devNpm("css-loader", "*")
    implementation devNpm("style-loader", "*")
    implementation devNpm("html-loader", "*")
    implementation devNpm("source-map-loader", "*")
    implementation devNpm("karma", "*")
}

kotlin {
    target {
        browser {
            webpackTask {
                doFirst {
                    def path = StringEscapeUtils.escapeJavaScript(file("src/main/resources").absolutePath)
                    file("webpack.config.d/resources.js").text = "config.resolve.modules.push('$path');"
                }
            }
        }
    }
}

compileKotlinJs {
    kotlinOptions {
        metaInfo = true
        outputFile = "$project.buildDir.path/js/${project.name}.js"
        sourceMap = true
        sourceMapEmbedSources = "always"
        moduleKind = 'commonjs'
        main = "call"
    }
}
